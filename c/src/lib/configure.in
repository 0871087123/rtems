dnl Process this file with autoconf to produce a configure script.
dnl
dnl $Id$

AC_PREREQ(2.13)
AC_INIT(libbsp)
RTEMS_TOP(../../..)
AC_CONFIG_AUX_DIR(../../..)

RTEMS_CANONICAL_TARGET_CPU
AM_INIT_AUTOMAKE(rtems-c-src-lib,RTEMS_VERSION,no)
AM_MAINTAINER_MODE

RTEMS_ENABLE_MULTIPROCESSING
RTEMS_ENABLE_NETWORKING
RTEMS_ENABLE_RDBG
RTEMS_ENABLE_LIBCDIR
RTEMS_ENABLE_BARE
RTEMS_ENABLE_HWAPI

RTEMS_ENV_RTEMSBSP

RTEMS_CHECK_CPU
RTEMS_CANONICAL_HOST

RTEMS_PROJECT_ROOT

dnl check target cc
RTEMS_PROG_CC_FOR_TARGET
RTEMS_CANONICALIZE_TOOLS

dnl if this is an i386, does gas have good code16 support?
RTEMS_I386_GAS_CODE16
AM_CONDITIONAL(RTEMS_GAS_CODE16,test "$RTEMS_GAS_CODE16" = "yes");

RTEMS_CHECK_CUSTOM_BSP(RTEMS_BSP)
RTEMS_CHECK_MULTIPROCESSING(RTEMS_BSP)
RTEMS_CHECK_NETWORKING(RTEMS_BSP)

AM_CONDITIONAL(UNIX,test "$RTEMS_CPU" = "unix")

# find all the CPU dependent library Makefiles
AC_CONFIG_SUBDIRS(libcpu)
AC_CONFIG_SUBDIRS(libbsp)

# If the TCP/IP stack is enabled, then find all TCP/IP Makefiles
if test "$HAS_NETWORKING" = "yes"; then
  if test "$RTEMS_HAS_RDBG" = "yes"; then
    RTEMS_CHECK_RDBG(RTEMS_BSP)
  fi
fi

if test "$HAS_RDBG" = "yes"; then
  cfg_subdirs="$cfg_subdirs librdbg"
fi

# If the HWAPI is enabled, the find the HWAPI Makefiles
RTEMS_ENABLE_HWAPI
if test "$RTEMS_HAS_HWAPI" = "yes"; then
  if test -d "${srcdir}/libhwapi"; then
    cfg_subdirs="$cfg_subdirs libhwapi"
  fi
  LIBHWAPI="libhwapi"
fi
AC_SUBST(LIBHWAPI)

if test -d "${srcdir}/start/$RTEMS_CPU"; then
cfg_subdirs="$cfg_subdirs start/$RTEMS_CPU"
RTEMS_CPU_STARTdir="$RTEMS_CPU"
fi
AC_SUBST(RTEMS_CPU_STARTdir)

if test "$RTEMS_CPU" != "unix"; then
## HACK: Suppress libchip for unix
cfg_subdirs="$cfg_subdirs libchip"
fi

AM_CONDITIONAL(HAS_NETWORKING,test "$HAS_NETWORKING" = "yes")
AM_CONDITIONAL(HAS_LIBCHIP,test "$RTEMS_CPU" != "unix")
AM_CONDITIONAL(HAS_RDBG,test "$HAS_RDBG" = "yes")

AC_CONFIG_SUBDIRS(libmisc)

AC_CONFIG_SUBDIRS($cfg_subdirs)

PROJECT_INCLUDE="\$(PROJECT_ROOT)/$RTEMS_BSP/lib/include"
AC_SUBST(PROJECT_INCLUDE)

# try to explicitly list a Makefile here 
AC_OUTPUT( 
Makefile
include/Makefile
include/sys/Makefile
include/motorola/Makefile
include/zilog/Makefile
include/rdbg/Makefile
include/rdbg/i386/Makefile
include/rdbg/powerpc/Makefile
libc/Makefile
start/Makefile
wrapup/Makefile)
