/*
 *  Linker script for Mongoose-V prototyping board.
 *  See README for address map details.
 *
 *  $Id$
 */

/*    . = 0x80020000; */

/*
 * Declare some sizes.
 */

_RamBase = DEFINED(_RamBase) ? _RamBase : 0x80000000;
_RamSize = DEFINED(_RamSize) ? _RamSize : 32M;
HeapSize = DEFINED(HeapSize) ? HeapSize : 0x40000;
_StackSize = DEFINED(_StackSize) ? _StackSize : 0x1000;
ClockRate = DEFINED(ClockRate) ? ClockRate : 12000000;



MEMORY
{
   romstore : ORIGIN = 0xbfc40000, LENGTH = 4M
   ram      : ORIGIN = 0x80020000, LENGTH = 4M
}

SECTIONS
{ 
    .text : 
    { 
       _ftext = . ;
      *(.init)
       eprol  =  .;
      *(.text)
      *(.text.*)
      *(.gnu.linkonce.t*)
      *(.mips16.fn.*)
      *(.mips16.call.*)
      PROVIDE (__runtime_reloc_start = .);
      *(.rel.sdata)
      *(.rel.dyn)
      PROVIDE (__runtime_reloc_stop = .);
      *(.fini)
    } >ram  AT>romstore

  .ctors    : 
  {
    /* gcc uses crtbegin.o to find the start of
       the constructors, so we make sure it is
       first.  Because this is a wildcard, it
       doesn't matter if the user does not
       actually link against crtbegin.o; the
       linker won't look for a file to match a
       wildcard.  The wildcard also means that it
       doesn't matter which directory crtbegin.o
       is in.  */

    KEEP (*crtbegin.o(.ctors))

    /* We don't want to include the .ctor section from
       from the crtend.o file until after the sorted ctors.
       The .ctor section from the crtend file contains the
       end of ctors marker and it must be last */

    KEEP (*(EXCLUDE_FILE (*crtend.o) .ctors))
    KEEP (*(SORT(.ctors.*)))
    KEEP (*(.ctors))
  } >ram  AT>romstore

  .dtors    : 
  {
    KEEP (*crtbegin.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o) .dtors))
    KEEP (*(SORT(.dtors.*)))
    KEEP (*(.dtors))

    etext  =  .;
    _etext  =  .;
  } >ram  AT>romstore

/*    . = .; */

  .rdata : 
  {
    *(.rdata)
    *(.rodata)
    *(.rodata.*)
    *(.gnu.linkonce.r*)
  } >ram  AT>romstore

  .data : 
  {
    _fdata = ALIGN(16);

    *(.data)
    *(.data.*)
    *(.gnu.linkonce.d*)
  } >ram  AT>romstore


  .lit8 : 
  {
    . = ALIGN(8);

    _gp = . + 0x8000;
    __global = _gp;
    *(.lit8)
  } >ram  AT>romstore

  .lit4 : 
  {
    *(.lit4)
  } >ram  AT>romstore 

  .sdata :   
  {
    *(.sdata)
    *(.sdata.*)
    *(.gnu.linkonce.s*)
  } >ram  AT>romstore

  .sbss : 
  {
  . = ALIGN(4);
   edata  =  .;
   _edata  =  .;
   _fbss = .;
    *(.sbss)
    *(.scommon)
  } >ram  AT>romstore 


  .bss : 
  {
    _bss_start = . ;
    *(.bss)
    *(.reginfo)
    *(COMMON)
    . = ALIGN (64);
    _stack_limit = .;
    . += _StackSize;
    __stack = .;
    _stack_init = .;
    _clear_end = .;
    HeapBase = .;
    . += HeapSize;    /* reserve some memory for heap */
    WorkspaceBase = .;
    end = .;
    _end = .;
  } >ram  AT>romstore 


/* 
** DWARF debug sections.
** Symbols in the DWARF debugging sections are relative to
** the beginning of the section so we begin them at 0.  
*/

  /* DWARF 1 */
  .debug          0 : { *(.debug) } AT>romstore
  .line           0 : { *(.line) } AT>romstore

  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) } AT>romstore
  .debug_sfnames  0 : { *(.debug_sfnames) } AT>romstore

  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) } AT>romstore
  .debug_pubnames 0 : { *(.debug_pubnames) } AT>romstore

  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info) }    AT>romstore
  .debug_abbrev   0 : { *(.debug_abbrev) }  AT>romstore
  .debug_line     0 : { *(.debug_line) }    AT>romstore
  .debug_frame    0 : { *(.debug_frame)}    AT>romstore
  .debug_str      0 : { *(.debug_str) }     AT>romstore
  .debug_loc      0 : { *(.debug_loc) }     AT>romstore
  .debug_macinfo  0 : { *(.debug_macinfo) } AT>romstore

  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) } AT>romstore
  .debug_funcnames 0 : { *(.debug_funcnames) } AT>romstore
  .debug_typenames 0 : { *(.debug_typenames) } AT>romstore
  .debug_varnames  0 : { *(.debug_varnames) }  AT>romstore
}
