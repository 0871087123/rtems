/*
 *  This file contains GNU linker directives for a generic MC68360 board.
 *
 * Saskatchewan Accelerator Laboratory
 * University of Saskatchewan
 * Saskatoon, Saskatchewan, CANADA
 * eric@skatter.usask.ca
 * 
 *  $Id$
 */

/*
 * Declare on-board memory
 */
MEMORY {
	  ram : ORIGIN = 0x00000000, LENGTH = 4M
	  rom : ORIGIN = 0xFF000000, LENGTH = 1M
	dpram : ORIGIN = 0xFE000000, LENGTH = 8k
}

/*
 * Declare some sizes
 */

/*
 *  XXX: The assignment of ". += XyzSize;" fails in older gld's if the
 *       number used there is not constant so the calculated sizes are 
 *       ignored below and the default number is used instead.
 */

HeapSize = DEFINED(HeapSize) ? HeapSize : 0x10000;
StackSize = DEFINED(StackSize) ? StackSize : 0x1000;

/*
 * Load objects
 */
SECTIONS {
        .text : {
		_RamBase = .;
		__RamBase = .;
		CREATE_OBJECT_SYMBOLS
		*(.text)
		. = ALIGN (16);
		_etext = .;
        } >ram 
        .data : {
		*(.data)
		. = ALIGN (16);
		_edata = .;
        } >ram
        .bss : {
		M68Kvec = .;
		_M68Kvec = .;
		. += (256 * 4);
		clear_start = .;
		*(.bss)
		*(COMMON)
		. = ALIGN (16);
		_end = .;

		_HeapStart = .;
		__HeapStart = .;
		/* XXX:  Temporary to get around a gld bug
		 *
		 *  . += HeapSize;
		 *  . += StackSize;
		 */
		. += 0x10000;   /* XXX */
		. += 0x1000;   /* XXX */

		. = ALIGN (16);
		stack_init = .;
		clear_end = .;

		_WorkspaceBase = .;
		__WorkspaceBase = .;
	} >ram

	/*
	 * On-chip memory/peripherals
	 */
	dpram : {
		m360 = .;
		_m360 = .;
		. += (8 * 1024);

	} >dpram

	/*
	 * Boot PROM
	 */
	rom : {
		_RomBase = .;
		__RomBase = .;
	} >rom
}
