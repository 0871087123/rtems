/*
 *  This file contains directives for the GNU linker which are specific
 *  to a gen8349eamds board
 *
 *  $Id$
 */

OUTPUT_FORMAT("elf32-powerpc", "elf32-powerpc",
              "elf32-powerpc")
OUTPUT_ARCH(powerpc)
 
ENTRY(start)

/*
 * Declare some sizes.
 * XXX: The assignment of ". += XyzSize;" fails in older gld's if the
 *      number used there is not constant.  If this happens to you, edit
 *      the lines marked XXX below to use a constant value.
 */
HeapSize = DEFINED(HeapSize) ? HeapSize    : 0x6800000;  /* 104M  Heap */
StackSize = DEFINED(StackSize) ? StackSize :   0x80000;  /* 512 kB   */
WorkSpaceSize = DEFINED(WorkSpaceSize) ? WorkSpaceSize : 0x80000; /* 512k */
RamDiskSize = DEFINED(RamDiskSize) ? RamDiskSize : 0x80000; /* 512 ram disk */

/* 
 * optionally reserve additional space 
 */
TopRamReserved = DEFINED(TopRamReserved) ? TopRamReserved : 0;
 
MEMORY
	{
	ram          : org = 0x0,        l = 256M
	mpc83xx_regs : org = 0xE0000000, l = 256k
	}


SECTIONS
{

    mpc83xx_regs (NOLOAD) :
    { 
    IMMRBAR = .;
    mpc83xx_regs*(.text)
    mpc83xx_regs*(.data)
    mpc83xx_regs*(.bss)
    mpc83xx_regs*(*COM*)
    } > mpc83xx_regs

   .resconf 0x000 :
    {
    *(.resconf)
    } > ram

   .vectors 0x100 :
    {
    *(.vectors)
    }   
    > ram

    /* 
     * The stack will live in this area - between the vectors and
     * the text section.
     */
	
    .text 0x10000:
    {
    _textbase = .;


    text.start = .;

    /* Entry point is the .entry section */
    *(.entry)
    *(.entry2)

    /* Actual Code */
    *(.text*)

    *(.rodata*)
    *(.rodata1)


    /*
     * Special FreeBSD sysctl sections.
     */
    . = ALIGN (16);
    __start_set_sysctl_set = .;
    *(set_sysctl_*);
    __stop_set_sysctl_set = ABSOLUTE(.);
    *(set_domain_*);
    *(set_pseudo_*);

    /* C++ constructors/destructors */
    *(.gnu.linkonce.t*)

    /*  Initialization and finalization code.
     *
     *  Various files can provide initialization and finalization functions.
     *  The bodies of these functions are in .init and .fini sections. We
     *  accumulate the bodies here, and prepend function prologues from
     *  ecrti.o and function epilogues from ecrtn.o. ecrti.o must be linked
     *  first; ecrtn.o must be linked last. Because these are wildcards, it
     *  doesn't matter if the user does not actually link against ecrti.o and
     *  ecrtn.o; the linker won't look for a file to match a wildcard.  The
     *  wildcard also means that it doesn't matter which directory ecrti.o
     *  and ecrtn.o are in. 
     */
    PROVIDE (_init = .);
    *ecrti.o(.init)
    *(.init)
    *ecrtn.o(.init)
    
    PROVIDE (_fini = .);
    *ecrti.o(.fini)
    *(.fini)
    *ecrtn.o(.init)

    /* 
     *  C++ constructors and destructors for static objects.
     *  PowerPC EABI does not use crtstuff yet, so we build "old-style"
     *  constructor and destructor lists that begin with the list lenght
     *  end terminate with a NULL entry.
     */
     
    PROVIDE (__CTOR_LIST__ = .);	     
    *crtbegin.o(.ctors)
    *(.ctors)
    *crtend.o(.ctors)
    LONG(0)
    PROVIDE (__CTOR_END__ = .);
	
    PROVIDE (__DTOR_LIST__ = .);
    *crtbegin.o(.dtors)
    *(.dtors)
    *crtend.o(.dtors)
    LONG(0)
    PROVIDE (__DTOR_END__ = .);
	
    /* Exception frame info */
    *(.eh_frame)

    /* Miscellaneous read-only data */
    _rodata_start = . ;
    *(.gnu.linkonce.r*)
    *(.lit)
    *(.shdata)
    *(.rodata)
    *(.rodata1)
    *(.descriptors)
    *(rom_ver)
    _erodata = .;

    PROVIDE (__EXCEPT_START__ = .);
    *(.gcc_except_table*)
    PROVIDE (__EXCEPT_END__ = .);
    __GOT_START__ = .;
    s.got = .;
    *(.got.plt) 
    *(.got)
    *(.got1)
    PROVIDE (__GOT2_START__ = .);
    PROVIDE (_GOT2_START_ = .);
    *(.got2)
    PROVIDE (__GOT2_END__ = .);
    PROVIDE (_GOT2_END_ = .);
    
    PROVIDE (__FIXUP_START__ = .);
    PROVIDE (_FIXUP_START_ = .);
    *(.fixup)
    PROVIDE (_FIXUP_END_ = .);
    PROVIDE (__FIXUP_END__ = .);
    

    /* Various possible names for the end of the .text section */
    etext = ALIGN(0x10);
    _etext = .;
    _endtext = .;
    text.end = .;
    PROVIDE (etext = .);
    PROVIDE (__etext = .);
 
    } > ram

  .jcr : { KEEP (*(.jcr)) } > ram

    .rel.dyn : {
      *(.rel.init)
      *(.rel.text .rel.text.* .rel.gnu.linkonce.t.*)
      *(.rel.fini)
      *(.rel.rodata .rel.rodata.* .rel.gnu.linkonce.r.*)
      *(.rel.data.rel.ro* .rel.gnu.linkonce.d.rel.ro.*)
      *(.rel.data .rel.data.* .rel.gnu.linkonce.d.*)
      *(.rel.tdata .rel.tdata.* .rel.gnu.linkonce.td.*)
      *(.rel.tbss .rel.tbss.* .rel.gnu.linkonce.tb.*)
      *(.rel.ctors)
      *(.rel.dtors)
      *(.rel.got)
      *(.rel.sdata .rel.sdata.* .rel.gnu.linkonce.s.*)
      *(.rel.sbss .rel.sbss.* .rel.gnu.linkonce.sb.*)
      *(.rel.sdata2 .rel.sdata2.* .rel.gnu.linkonce.s2.*)
      *(.rel.sbss2 .rel.sbss2.* .rel.gnu.linkonce.sb2.*)
      *(.rel.bss .rel.bss.* .rel.gnu.linkonce.b.*)
    } >ram
    .rela.dyn : {
      *(.rela.init)
      *(.rela.text .rela.text.* .rela.gnu.linkonce.t.*)
      *(.rela.fini)
      *(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*)
      *(.rela.data .rela.data.* .rela.gnu.linkonce.d.*)
      *(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*)
      *(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*)
      *(.rela.ctors)
      *(.rela.dtors)
      *(.rela.got)
      *(.rela.got1)
      *(.rela.got2)
      *(.rela.sdata .rela.sdata.* .rela.gnu.linkonce.s.*)
      *(.rela.sbss .rela.sbss.* .rela.gnu.linkonce.sb.*)
      *(.rela.sdata2 .rela.sdata2.* .rela.gnu.linkonce.s2.*)
      *(.rela.sbss2 .rela.sbss2.* .rela.gnu.linkonce.sb2.*)
      *(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
    } >ram

    PROVIDE (__SDATA2_START__ = .);
  .sdata2   	  : { *(.sdata2) *(.gnu.linkonce.s2.*)	} >ram
  .sbss2   	  : { *(.sbss2) *(.gnu.linkonce.sb2.*)	} >ram
    PROVIDE (__SBSS2_END__ = .);
	
    .sbss2   	  : { *(.sbss2) 	} >ram
    PROVIDE (__SBSS2_END__ = .);
	
    /* R/W Data */
    .data ( . ) :
    {
      . = ALIGN (4);

      data.start = .;

      *(.data)
      *(.data1)
      *(.data.* .gnu.linkonce.d.*)
      PROVIDE (__SDATA_START__ = .);
      *(.sdata*)
      *(.gnu.linkonce.s.*)
      data.end = .;
    } > ram

    __SBSS_START__ = .;
    .bss :
    {
      bss.start = .;
      *(.bss .bss* .gnu.linkonce.b*)
      *(.sbss*) *(COMMON)
      . = ALIGN(4);
      bss.end = .;
    } > ram
    __SBSS_END__ = .;

    PROVIDE(_bss_start   = ADDR(.bss));
    PROVIDE(_bss_size    = SIZEOF(.bss));
    PROVIDE(_data_start  = ADDR(.data));
    PROVIDE(_data_size   = SIZEOF(.data));
    PROVIDE(_text_start  = ADDR(.text));
    PROVIDE(_text_size   = SIZEOF(.text));
    PROVIDE(_end = data.end);

    .gzipmalloc : {
	. = ALIGN (16);
        _startmalloc = .;
     } >ram
          	

    /*
     * Interrupt stack setup
     */
    IntrStack_start = ALIGN(0x10);
    . += 0x4000;
    intrStack = .;
    PROVIDE(intrStackPtr = intrStack);




    _WorkspaceBase = .;
    __WorkspaceBase = .;
    . += WorkSpaceSize;

    _RamDiskBase = .;
    __RamDiskBase = .;
    . += RamDiskSize;
    _RamDiskEnd  = .;
    __RamDiskEnd = .;
    PROVIDE( _RamDiskSize = _RamDiskEnd - _RamDiskBase );

    _HeapStart = .;
    __HeapStart = .;
    . += HeapSize;
    _HeapEnd = .;
    __HeapEnd = .;

    clear_end = .;

    /* Sections for compressed .text and .data         */
    /* after the .datarom section is an int specifying */
    /* the length of the following compressed image    */
    /* Executes once then could get overwritten        */
    .textrom 0x100000 :
    {
    	*(.textrom)
    	_endloader = .;
    } > ram

    .datarom :
    {
    	_dr_start = .;
    	*(.datarom)
    	_dr_end = .;
    } > ram
    dr_len = _dr_end - _dr_start;


    .line 0 : { *(.line) }
    .debug 0 : { *(.debug) }
    .debug_sfnames 0 : { *(.debug_sfnames) }
    .debug_srcinfo 0 : { *(.debug_srcinfo) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_aranges 0 : { *(.debug_aranges) }
    .debug_aregion 0 : { *(.debug_aregion) }
    .debug_macinfo 0 : { *(.debug_macinfo) }
    .stab 0 : { *(.stab) }
    .stabstr 0 : { *(.stabstr) }
}
